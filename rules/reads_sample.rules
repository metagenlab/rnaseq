
rule extract_mapped_read:
    conda:
        "../envs/samtools.yml"
    input:
        "samples/{sample}/mapping/bwa/reference_genome.bam"
    output:
        "samples/{sample}/mapping/bwa/reference_genome_mapped.bam"
    shell:
        """
        samtools view -b -F4 {input[0]} > {output[0]}
        """

rule convert_bam_to_fastq:
    conda:
        "../envs/bedtools.yml"
    input:
        "samples/{sample}/mapping/bwa/reference_genome_mapped.bam"
    output:
        "samples/{sample}/mapping/bwa/reference_genome_mapped.fastq"
    shell:
        """
        bedtools bamtofastq -i {input[0]} -fq {output[0]}
        """

rule downsample_reads:
    conda:
        "../envs/seqtk.yml"
    input:
        "samples/{sample}/mapping/bwa/reference_genome_mapped.fastq"
    output:
        "samples/{sample}/mapping/bwa/reference_genome_mapped_sample_{count}.fastq"
    shell:
        """
        seqtk sample -s100 {input[0]} {wildcards.count} > {output[0]}
        """

checkpoint mapped_reads_counts:
    input:
        "samples/{sample}/mapping/bwa/reference_genome_mapped.fastq",
    output:
        "samples/{sample}/mapping/bwa/read_count.txt"
    shell:
        "echo $(cat {input}|wc -l)/4|bc > {output}"





# input function for the rule aggregate
def aggregate_input(wildcards):
    # decision based on content of output file
    with open(checkpoints.mapped_reads_counts.get(sample=wildcards.sample).output[0]) as f:
        n_reads = int(f.read().strip())
        lst = []
        for i in range(500000, n_reads, 500000):
            lst.append("samples/{sample}/rnaseq/htseq/sample_%s_counts.txt" % i)
        lst.append("samples/{sample}/rnaseq/htseq/sample_%s_counts.txt" % n_reads)
    return lst


rule get_htseq_stats:
    input:
        gene_counts = aggregate_input
    output:
        "aggregated/htseq_{sample}.tab"
    script: "scripts/plot_rarefaction.py"

rule merge_htseq_stats:
    input:
        file_list = expand("aggregated/htseq_{sample}.tab", sample=read_naming.keys())
    output:
        "aggregated/merged_htseq_data.tab"
    shell:
        """
        echo -e "sample\tn_reads\t_not_aligned\t__no_feature\ttotal\tequal_0\tlarger_than_0\tlarger_than_10\tlarger_than_100\tlarger_than_1000\tlarger_than_10000" >> {output[0]}
        for file in {input}; do
             cat $file >> {output[0]}
        done
        """

rule plot_rarefaction_curve:
    conda:
        "../envs/ggplot2-gridextra.yml"
    input:
        "aggregated/merged_htseq_data.tab"
    output:
        "aggregated/rarefaction.svg",
        "aggregated/rarefaction.pdf"
    script: "scripts/plot_rarefaction.R"
