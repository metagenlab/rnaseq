rule merge_counts:
    conda:
        "../envs/edger.yml"
    input:
        htseq_files = expand("samples/{sample}/rnaseq/htseq/counts.txt", sample=read_naming.keys()),
        reference_gff = "reference/reference_genome.gff",
    output:
        "report/rnaseq/edger/count_table.tsv",
        "report/rnaseq/edger/cor_plot.pdf",
        "report/rnaseq/edger/counts_distribution.pdf"
    script: "scripts/merge_counts.R"


rule rpkm_table:
    conda:
        "../envs/edger.yml"
    input:
        count_file = "report/rnaseq/edger/count_table.tsv",
        reference_gff = "reference/reference_genome.gff",
    params:
        sample_table = config["local_samples"],
        annotation = "annotations/merged_annotation.tab",
    output:
        "report/rnaseq/edger/rpkm_table.tsv",
    script: "scripts/rpkm.R"


if config["replicates"]:
    print("------ replicates ------")

    rule edger_analysis:
        conda:
            "../envs/edger.yml"
        input:
            count_file = "report/rnaseq/edger/count_table.tsv",
            reference_gff = "reference/reference_genome.gff",
        params:
            sample_table = config["local_samples"],
            annotation = "annotations/merged_annotation.tab",
            FDR_cutoff = config["FDR_cutoff"],
            logFC_cutoff = config["logFC_cutoff"]
        output:
            "report/rnaseq/edger/{condition_1}_vs_{condition_2}/{condition_1}_vs_{condition_2}_plotMDS.pdf",
            "report/rnaseq/edger/{condition_1}_vs_{condition_2}/{condition_1}_vs_{condition_2}_volcano_FDR0.01_logFC2.pdf",
            "report/rnaseq/edger/{condition_1}_vs_{condition_2}/{condition_1}_vs_{condition_2}_volcano_FDR%s_logFC%s.pdf" % (config["FDR_cutoff"],
                                                                                                                             config["logFC_cutoff"]),
            "report/rnaseq/edger/{condition_1}_vs_{condition_2}/{condition_1}_vs_{condition_2}_plotSmear_FDR0.01.pdf",
            "report/rnaseq/edger/{condition_1}_vs_{condition_2}/{condition_1}_vs_{condition_2}_heatmap_top_100_FRD%s_logFC%s.svg" % (config["FDR_cutoff"],
                                                                                                                                     config["logFC_cutoff"]),
            "report/rnaseq/edger/{condition_1}_vs_{condition_2}/{condition_1}_vs_{condition_2}_counts.tsv",
            "report/rnaseq/edger/{condition_1}_vs_{condition_2}/{condition_1}_vs_{condition_2}_heatmap_top_100_down_FRD%s_logFC%s.svg" % (config["FDR_cutoff"],
                                                                                                                                          config["logFC_cutoff"]),
            "report/rnaseq/edger/{condition_1}_vs_{condition_2}/{condition_1}_vs_{condition_2}_heatmap_top_100_up_FRD%s_logFC%s.svg" % (config["FDR_cutoff"],
                                                                                                                                        config["logFC_cutoff"]),

        script: "scripts/edger_analysis.R"

else:
    print("------ no replicates ------")
    rule edger_analysis_no_replicates:
        conda:
            "../envs/edger.yml"
        input:
            count_file = "report/rnaseq/edger/count_table.tsv",
            reference_gff = "reference/reference_genome.gff",
        params:
            sample_table = config["local_samples"],
            annotation = "annotations/merged_annotation.tab",
        output:
            "report/rnaseq/edger/{condition_1}_vs_{condition_2}/{condition_1}_vs_{condition_2}_plotMDS.pdf",
            "report/rnaseq/edger/{condition_1}_vs_{condition_2}/{condition_1}_vs_{condition_2}_plotSmear.pdf",
            "report/rnaseq/edger/{condition_1}_vs_{condition_2}/{condition_1}_vs_{condition_2}_counts.tsv",
            "report/rnaseq/edger/{condition_1}_vs_{condition_2}/{condition_1}_vs_{condition_2}_heatmap_top_100.pdf",
            "report/rnaseq/edger/{condition_1}_vs_{condition_2}/{condition_1}_vs_{condition_2}_heatmap_top_100_down.pdf",
            "report/rnaseq/edger/{condition_1}_vs_{condition_2}/{condition_1}_vs_{condition_2}_heatmap_top_100_up.pdf",

        script: "scripts/edger_analysis_no_replicates.R"

